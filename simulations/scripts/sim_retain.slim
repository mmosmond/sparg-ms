initialize() {

	defineConstant("data_dir", 'data/');
	fname = ("retain_slim_" + asInteger(L) + "L_" + RBP + "RBP_" + format("%.1f", LAMBDA) + "LAMBDA_" +
		 format("%.1f", K) + "K_" + W + "W_" + SIGMAi + "SIGMAi_" +
		 SIGMAd + "SIGMAd_" + MAXT + "MAXT_" + nrep + "nrep");
	defineConstant("filename", fname);
	defineConstant("output", data_dir + "/" + filename + ".trees");
        defineConstant("retain_generations", MAXT - retain_gens); //flip so retain_gens refers to generations in the past
        initializeSLiMModelType("nonWF"); // non Wright Fisher
        initializeSLiMOptions(dimensionality="xy"); // two spatial dimensions
        initializeTreeSeq(retainCoalescentOnly=F); // record the true tree sequence (can also speed things up), keep unary nodes when simplifying

        initializeMutationRate(0.0); // no mutations (add these in msprime)
        initializeMutationType("m1", 0.5, "f", 0.0); // irrelevant mutation type
        initializeGenomicElementType("g1", m1, 1.0); // irrelevant genome type
        initializeGenomicElement(g1, 0, asInteger(L)-1); // length of chromosome
        initializeRecombinationRate(RBP); // recombination rate per base

        // spatial interaction for local competition
        initializeInteractionType("i1", "xy", reciprocal=T, maxDistance = 3*SIGMAi); // define interaction type i1, in two spatial dimensions, where individual A has the same effect on B that B does on A (this speeds up computation), and only individuals within distance 3*SIGMA interact (again to speed things up)  
        i1.setInteractionFunction("n", 1.0/(2*PI*SIGMAi^2), SIGMAi); // convert distance to interaction strength using a Gaussian (n for normal), with maximum value 1/(2pi*sigma^2) and standard deviation sigma (ie, this is truly and normal PDF with mean 0 and variance sigma^2)
        
        // spatial interaction for mate choice
        initializeInteractionType("i2", "xy", reciprocal=T, maxDistance = 3*SIGMAd); // define interaction type i2, in two spatial dimensions, where individual A has the same effect on B that B does on A (this speeds up computation), and only individuals within distance 3*SIGMA interact (again to speed things up)  
        i2.setInteractionFunction("n", 1.0/(2*PI*SIGMAd^2), SIGMAd); // convert distance to interaction strength using a Gaussian (n for normal), with maximum value 1/(2pi*sigma^2) and standard deviation sigma (ie, this is truly and normal PDF with mean 0 and variance sigma^2)
    }

    reproduction() {
        neighbor_density = i1.totalOfNeighborStrengths(individual); // sum of interaction strengths
        num_offspring = rpois(1, LAMBDA / (1 + neighbor_density / K)); // poisson number of offspring with mean LAMBDA/(1+n_d/K), ie Beverton-Holt density dependence
        mate = i2.drawByStrength(individual, 1);  // single mate for all offspring (ie monogamy), with mate chosen randomly based on interaction strength
        if (size(mate) > 0) { // if there is a mate (possible none within interacting distance, in which case there are no offspring produced)
            for (k in seqLen(num_offspring)) {
                offspring = p1.addCrossed(individual, mate); //make offspring by sexual reproduction
                pos = individual.spatialPosition + rnorm(2, 0, SIGMAd); // set position of offspring as random normal in both directions
                offspring.setSpatialPosition(p1.pointReflected(pos)); // put offspring in its place
            }
        }
    }
    
    1 early() {
        sim.rescheduleScriptBlock(s1, start=MAXT, end=MAXT); //define end point of sim
	sim.addSubpop("p1", asInteger(round(K * W * W))); //set population size at carrying capacity
        p1.setSpatialBounds(c(0.0, 0.0, W, W)); //set spatial plane
        p1.individuals.setSpatialPosition(p1.pointUniform(asInteger(round(K*W*W)))); // start with uniform distribution across range
    }

    early() { // survival probabilities
        p1.fitnessScaling = 1; //all individuals have same fitness (neutral)
        inds = sim.subpopulations.individuals;
        inds[inds.age > 0].fitnessScaling = 0.0; //remove adults to make discrete generations
    }

    late() {
        i1.evaluate(); //calculate interactions
    	i2.evaluate();
        if (sim.generation % 1000 == 0) {
	   catn(sim.generation + "/" + MAXT);
        }
        if (any(retain_generations == sim.generation)) {
           sim.treeSeqRememberIndividuals(p1.individuals, permanent=F); //remember individuals only if ancestors of final population
	}
     }

    s1 late () {
        sim.treeSeqOutput(output); //output treesequence
        catn("Done.");
        sim.simulationFinished();
    }
